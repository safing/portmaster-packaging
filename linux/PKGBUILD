# Maintainer: Patrick Pacher <patrick@safing.io>
pkgname=portmaster
pkgver=0.5.2
pkgrel=1
pkgdesc='Application Firewall: Block Mass Surveillance - Love Freedom'
arch=('x86_64')
license=('AGPL3')
install=arch.install
depends=('libnetfilter_queue' 'webkit2gtk')
makedepends=('imagemagick') # for convert
optdepends=('libappindicator-gtk3: for systray indicator')
options=('!strip')
#changelog=
source=("portmaster-start::https://updates.safing.io/linux_amd64/start/portmaster-start_v${pkgver//./-}"
		'portmaster.desktop'
		'portmaster_notifier.desktop'
		'portmaster_logo.png'
		"debian/portmaster.service")
noextract=('portmaster-start')
md5sums=('e267b0b2913fc84babcd805264b2d0f7'
         '19864fff9d542c427acb727636ac5390'
         'cebfc4aa5f12a1da9b9ebf70f26f0d6f'
         'c5484dd4e42606f8141abdc4e04d5d61'
         'a568e92f6a6f219ded28d8bfd1d6e1f5')

prepare() {
	for res in 16 32 48 96 128 ; do
		local iconpath=${pkgname}-${pkgver}/icons/${res}x${res}/apps
		mkdir -p ${iconpath} ; 
		convert ./portmaster_logo.png -resize ${res}x${res} ${iconpath}/portmaster.png ; 
	done
}

build() {
	mkdir -p ${pkgname}-${pkgver}/data
	chmod a+x "${srcdir}/portmaster-start"
	"${srcdir}/portmaster-start" --data ${pkgname}-${pkgver}/data update
}

package() {
	install -dm 755 "${pkgdir}/var/lib/portmaster/"
	install -dm 755 "${pkgdir}/usr/lib/systemd/system/"
	install -dm 755 "${pkgdir}/usr/share/icons/hicolor/"
	install -dm 755 "${pkgdir}/usr/share/applications/"
	cp "${srcdir}/portmaster.service" "${pkgdir}/usr/lib/systemd/system/portmaster.service"
	cp "${srcdir}/portmaster-start" "${pkgdir}/var/lib/portmaster/"
	cp -r "${srcdir}/${pkgname}-${pkgver}/data/"* "${pkgdir}/var/lib/portmaster/"
	cp -r "${srcdir}/${pkgname}-${pkgver}/icons/"* "${pkgdir}/usr/share/icons/hicolor/"
	cp "${srcdir}/portmaster.desktop" "${pkgdir}/usr/share/applications/"
	cp "${srcdir}/portmaster_notifier.desktop" "${pkgdir}/usr/share/applications/"
}
